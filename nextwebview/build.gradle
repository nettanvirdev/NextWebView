plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    namespace 'com.levelpixel.nextwebview'
    compileSdk 35

    defaultConfig {
        minSdk 26
        targetSdk 35
    }

    buildFeatures {
        buildConfig = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    
    publishing {
        singleVariant('release') {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                
                groupId = 'com.github.nettanvirdev'
                artifactId = 'nextwebview'
                version = '1.0.0'
                
                pom {
                    name = 'NextWebView'
                    description = 'A secure, enhanced WebView component for Android with built-in ad blocking and security features'
                    url = 'https://github.com/nettanvirdev/nextwebview'
                    licenses {
                        license {
                            name = 'Custom License'
                            url = 'https://github.com/nettanvirdev/nextwebview/blob/master/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'nettanvirdev'
                            name = 'Next Developer'
                        }
                    }
                }
            }
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
}